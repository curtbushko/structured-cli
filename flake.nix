{
  description = "Golang flake";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
  inputs.golang-shared-configs.url = "github:curtbushko/golang-shared-configs";

  outputs = { self, nixpkgs, golang-shared-configs }:
    let
      goVersion = 25; # Change this to update the whole stack

      supportedSystems = [ "x86_64-linux" "aarch64-linux" "x86_64-darwin" "aarch64-darwin" ];
      forEachSupportedSystem = f: nixpkgs.lib.genAttrs supportedSystems (system: f {
        inherit system;
        pkgs = import nixpkgs {
          inherit system;
          config.allowUnfree = true;
          overlays = [ self.overlays.default ];
        };
      });

      # Build go-ai-lint from source
      go-ai-lint = { pkgs }: pkgs.buildGoModule {
        pname = "go-ai-lint";
        version = "1.0.0";
        src = pkgs.fetchFromGitHub {
          owner = "curtbushko";
          repo = "go-ai-lint";
          rev = "v1.0.0";
          sha256 = "sha256-y2G7dTZqM/rEQaALu54bHigBeO1xxRIblBJ7QxOffW4=";
        };
        subPackages = [ "cmd/go-ai-lint" ];
        vendorHash = "sha256-zkXyXTEnMmBZnvzoq0UWKgzWZlyNRyQZCYAv+huZo0I=";
      };

      # Build godog BDD testing tool from source
      godog = { pkgs }: pkgs.buildGoModule {
        pname = "godog";
        version = "0.15.0";
        src = pkgs.fetchFromGitHub {
          owner = "cucumber";
          repo = "godog";
          rev = "v0.15.0";
          sha256 = "sha256-iwWMhXMqjRRxC2hvuKwtfm9Y+ROzNJjqMoI7P27xSfY=";
        };
        subPackages = [ "cmd/godog" ];
        vendorHash = "sha256-lPnXxbc4A9yNwpzWkuXJ6hd2adOeOF1+LlEIrLAlyEI=";
      };
    in
    {
      overlays.default = final: prev: {
        go = final."go_1_${toString goVersion}";
      };

      devShells = forEachSupportedSystem ({ pkgs, system }:
        let
          sharedConfigs = golang-shared-configs.packages.${system}.all-configs;
        in {
        default = pkgs.mkShell {
          packages = with pkgs; [
            # Go development
            go
            go-task
            gotools
            golangci-lint
            (go-ai-lint { inherit pkgs; })
            (godog { inherit pkgs; })
            sharedConfigs

            # E2E testing dependencies
            # Core tools for file operations tests
            coreutils
            findutils
            gnugrep
            ripgrep
            fd

            # Git for git command tests
            git

            # Build tools for make/just tests
            gnumake
            just

            # Docker for container tests
            docker

            # Node.js for npm tests (optional)
            nodejs

            # Python for python tool tests (optional)
            python3
            python3Packages.pip

            # Rust for cargo tests (optional)
            rustc
            cargo
          ];

          shellHook = ''
            cp -f ${sharedConfigs}/.golangci.yml .golangci.yml
            cp -f ${sharedConfigs}/.go-arch-lint.yml .go-arch-lint.yml
            cp -f ${sharedConfigs}/.go-ai-lint.yml .go-ai-lint.yml
          '';
        };
      });
    };
}
